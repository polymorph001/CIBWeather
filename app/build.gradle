apply plugin: 'com.android.application'

def localPropertiesFile = rootProject.file("local.properties")
def localProperties = new Properties()
localProperties.load(localPropertiesFile.newDataInputStream())

buildscript {
    ext {
        android_support_library_version = '27.1.0'
        dagger2_version = '2.14.1'
        okhttp3_version = '3.10.0'
        retrofit2_version = '2.3.0'
        autovalue_version = '1.5'
        joda_android_version = '2.9.9'
    }
}

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.mitchwongho.example.darksky"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [googlePlacesApiKey: "${localProperties.getProperty("places.api.key")}"]
        buildConfigField "String", "ApiKey", "\"${localProperties.getProperty("darksky.api.key")}\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"

    productFlavors {
        develop {
            versionNameSuffix "-dev"
            buildConfigField "String", "ApiUrl", '"https://api.darksky.net/forecast/"'
            dimension "default"
        }
        qa {
            versionNameSuffix "-qa"
            buildConfigField "String", "ApiUrl", '"https://api.darksky.net/forecast/"'
            dimension "default"
        }
        production {
            buildConfigField "String", "ApiUrl", '"https://api.darksky.net/forecast/"'
            dimension "default"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$android_support_library_version"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation "com.android.support:appcompat-v7:$android_support_library_version"
    implementation "com.android.support:cardview-v7:$android_support_library_version"
    implementation "com.android.support:recyclerview-v7:$android_support_library_version"
    implementation "com.android.support:design:$android_support_library_version"
    implementation 'javax.inject:javax.inject:1'
    implementation 'com.jakewharton.timber:timber:4.6.1'
    implementation "net.danlew:android.joda:$joda_android_version"
    //
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
// Because RxAndroid releases are few and far between, it is recommended you also
// explicitly depend on RxJava's latest version for bug fixes and new features.
// (see https://github.com/ReactiveX/RxJava/releases for latest 2.x.x version)
    implementation 'io.reactivex.rxjava2:rxjava:2.1.10'
    implementation "com.squareup.retrofit2:retrofit:$retrofit2_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit2_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit2_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp3_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp3_version"
    //
    implementation "com.google.dagger:dagger-android:$dagger2_version"
    implementation "com.google.dagger:dagger-android-support:$dagger2_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger2_version"
    annotationProcessor "com.google.dagger:dagger-android-processor:$dagger2_version"
    implementation("com.google.dagger:dagger:$dagger2_version") {
        exclude group: "javax.inject"
    }
    //
    provided "com.google.auto.value:auto-value:$autovalue_version"
    annotationProcessor "com.google.auto.value:auto-value:$autovalue_version"
    //
    implementation "com.google.android.gms:play-services-places:11.8.0"
    //
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}